import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.3"
    id "com.jfrog.bintray" version "1.7.1"
    id "com.diffplug.gradle.spotless" version "2.4.0"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'jacoco'

version = '0.19'
sourceCompatibility = 10

project.group = 'ubicrypt'
mainClassName = 'ubicrypt.UbiCrypt'

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'ubicrypt'
            artifactId 'ubicrypt'
            version "${version}"
        }
    }
}
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'ubicrypt'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/gfrison/ubicrypt.git'
    }
}



jar {
    manifest {
        attributes("Implementation-Title": "UbiCrypt",
                "Implementation-Version": version)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'http://repo.spring.io/libs-release' }
    maven { url "https://repo.spring.io/libs-snapshot" }
    maven { url "http://repo.spring.io/milestone" }
    maven { url "http://dl.bintray.com/ubicrypt/maven" }
    maven { url 'http://repo.spring.io/snapshot' }
    maven { url "https://oss.sonatype.org/content/groups/public" }
    jcenter()
}


ext {
    jacksonVersion = '2.9.4'
    springVersion = '1.3.8.RELEASE'
    awsVersion = '1.11.297'
    gdriveVersion = '1.23.0'
}



dependencies {
    compile "javax.annotation:javax.annotation-api:1.3.1"
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile "org.springframework.boot:spring-boot:$springVersion"
    compile "org.springframework.boot:spring-boot-autoconfigure:$springVersion"
    compile "org.springframework.boot:spring-boot-starter-log4j:$springVersion"
    compile("org.springframework.boot:spring-boot-starter:$springVersion") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    compile "org.springframework.boot:spring-boot-configuration-processor:$springVersion"
    testCompile "org.springframework.boot:spring-boot-starter-test:$springVersion"

    compile 'org.bouncycastle:bcpg-jdk15on:1.56'
    compile 'com.google.guava:guava:19.0'
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    compile "com.fasterxml.jackson.module:jackson-module-afterburner:$jacksonVersion"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-smile:$jacksonVersion"


    compile 'io.reactivex:rxjava:1.2.3'
    compile 'javax.inject:javax.inject:1'
    compile 'commons-net:commons-net:3.5'
    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.commons:commons-lang3:3.4'

    // Reactor Core
    compile 'io.projectreactor:reactor-core:2.0.7.RELEASE'

    compile 'org.controlsfx:controlsfx:8.40.12'
    compile 'de.jensd:fontawesomefx-commons:8.13'
    //AWS
    compile "com.amazonaws:aws-java-sdk-s3:$awsVersion"
    compile "com.thoughtworks.xstream:xstream:1.4.10"
    compile "javax.xml.bind:jaxb-api:2.3.0"
    compile "com.sun.xml.bind:jaxb-core:2.3.0"
    compile "com.sun.xml.bind:jaxb-impl:2.3.0"
    compile "javax.activation:activation:1.1.1"

    //google drive
    compile 'com.google.apis:google-api-services-drive:v3-rev107-1.23.0'
    compile "com.google.http-client:google-http-client:$gdriveVersion"
    compile "com.google.http-client:google-http-client-jackson2:$gdriveVersion"
    compile "com.google.oauth-client:google-oauth-client-jetty:$gdriveVersion"





    testCompile 'junit:junit:4.11'
    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile 'org.junit:junit5-api:5.0.0-SNAPSHOT'
    testCompile "de.saxsys:jfx-testrunner:1.0"

}


task packageNative(type: Exec, dependsOn: shadowJar) {
    def updatefxVersion = 1

    if (Os.isFamily(Os.FAMILY_MAC))
        executable './package/mac.sh'
    else if (Os.isFamily(Os.FAMILY_UNIX))
        executable './package/linux.sh'
    else if (Os.isFamily(Os.FAMILY_WINDOWS))
        executable './package/windows.ps1'
    else
        throw new GradleException("Unsupported OS: " + System.properties['os.name'])

    args updatefxVersion, shadowJar.archivePath, mainClassName
}


spotless {
    java {
        googleJavaFormat('1.1')
        licenseHeaderFile 'lic-header.java'
    }
}

compileJava.dependsOn 'spotlessApply'

ext.moduleName = 'ubicrypt'
compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

def java_home = System.getenv('JAVA_HOME')
task link(type: Exec) {
    dependsOn 'clean'
    dependsOn 'jar'

    workingDir 'build'

    commandLine "${java_home}/bin/jlink", '--module-path', "libs${File.pathSeparatorChar}${java_home}/jmods",
            '--add-modules', 'ubicrypt', '--launcher', "ubicrypt=${mainClassName}", '--output', 'dist', '--strip-debug',
            '--compress', '2', '--no-header-files', '--no-man-pages'
}
